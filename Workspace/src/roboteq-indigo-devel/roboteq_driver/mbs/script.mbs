SCRIPT_VER=32
MAX_CHANNELS=2
Dim last_mode[2] as Integer

' Mode values are:
'  -1 stopped
'  0 speed mode
'  1 position mode
' These correspond to the constants in roboteq_msgs/Command. The motor begins in stopped
' mode, the setup for which is run when last_mode[0] != VAR[1].
last_mode[0] = -127
last_mode[1] = -127
setcommand(_VAR, 1, -1)
setcommand(_VAR, 2, -1)

' Motor controller configuration. Much of this is particular to Grizzly
' and can be generalized/parameterized as the need arises. Page numbers
' are a reference to this document:
' www.roboteq.com/index.php/docman/motor-controllers-documents-and-files/documentation/user-manual/7-nextgen-controllers-user-manual/file

setconfig(_ECHOF, 1) ' Serial echo off (p195)
setconfig(_RWD, 110) ' Watchdog timeout, milliseconds (p195)
setconfig(_AMOD, 1, 1) ' Analog input 1 absolute mode
setconfig(_AMOD, 2, 1) ' Analog input 2 absolute mode

'set encoder mode (p315)
setconfig(_EMOD, 1, 18)
setconfig(_EMOD, 2, 34)

' Undervoltage/overvoltage limits (p214)
setconfig(_OVL, 700)
setconfig(_UVL, 100)

for ch = 1 andwhile ch <= MAX_CHANNELS evaluate ch++
	' Encoder, speed config (p208, p221)
	setconfig(_EPPR, ch, 512) 'encoder PPR
	setconfig(_MXRPM, ch, 5600) 'MAX RPM 5600 RPM
	setconfig(_CLERD, ch, 2) 'closed loop error detection
	
	
	setconfig(_BLSTD, ch, 2) ' stall detection, 500ms at 25% Power 

	' Current limit (p216)
	setconfig(_ALIM, ch, 1000) 'max current 100A
	'setconfig(_ATGA, ch, 2000) 'action to take when current is too high
	'setconfig(_ATGD, ch, 50) 'how long to delay after the action should take place
	'setconfig(_ATRIG, ch, 0) 'what current to trigger at

	' acceleration/deceleration rate (p219)
	setconfig(_MAC, ch, 60000) '6000 RPM/s
	setconfig(_MDEC, ch, 60000)
next

top:
	for ch = 1 andwhile ch <= MAX_CHANNELS evaluate ch++
		if last_mode[ch - 1] <> getvalue(_VAR, ch) then
			last_mode[ch - 1] = getvalue(_VAR, ch)

			' Motor to be stopped (high-Z).
			if getvalue(_VAR, ch) = -1 then
				'setconfig(_MMOD, ch, 0)
				'setconfig(_KP, ch, 0)
				'setconfig(_KI, ch, 0)
				'setconfig(_KD, ch, 0)
				'setconfig(_ICAP, ch, 0)
				setcommand(_G, ch, 0)
				'setcommand(_MSTOP, ch)
			end if

			' Motor in velocity-control mode.
			if getvalue(_VAR, ch) = 0 then
				setconfig(_MMOD, ch, 1) '1 is closed loop speed control, 0 is open loop
				setconfig(_KP, ch, 1)'gain multiplied by 10
				setconfig(_KI, ch, 15)
				setconfig(_KD, ch, 0)
				setconfig(_ICAP, ch, 100)
				'setcommand(_MGO,1)
			end if

			' Motor in position-control mode.
			if getvalue(_VAR, ch) = 1 then
				'setconfig(_MMOD, ch, 3)
				'setconfig(_KP, ch, 2)
				'setconfig(_KI, ch, 0)
				'setconfig(_KD, ch, 0)
				'setconfig(_ICAP, ch, 0)
			end if
		end if
		
		' Send feedback for this channel at 25Hz.
		GoSub PrintFeedback
	next

	' Send status messages for the controller at 5Hz.
	c++
	if c = 5 then
		GoSub PrintStatus
		c = 0
	end if

	wait(19)
	goto top

PrintFeedback:
	print("&f:", ch)
	print(":", getvalue(_MOTAMPS, ch))
	print(":", getvalue(_MOTCMD, ch))
	print(":", getvalue(_MOTPWR, ch))
	print(":", getvalue(_ABSPEED, ch))
	print(":", getvalue(_ABCNTR, ch))
	print(":", getvalue(_VOLTS, 2))
	print(":", getvalue(_BATAMPS, ch))
	print(":", getvalue(_ANAIN, ch))
	print(":", getvalue(_TEMP, ch+1))
	'_ABCNTR for absolute encoder count
	'_RELCNTR for relative encoder count -> counts since last time called
	'_ABSPEED for absolute speed in RPM -> must set Pulses per Revolution
	'_RELSPEED for relative speed between 0-1000 -> dependent on the MAX RPM value
	print("\r")
	Return
	
PrintStatus:
	print("&s:", SCRIPT_VER)
	print(":", getvalue(_FLTFLAG))
	print(":", getvalue(_STFLAG))
	print(":", getvalue(_VOLTS, 1))
	print(":", getvalue(_VOLTS, 3))
	print(":", getvalue(_TEMP, 1))
	print("\r")
	Return


